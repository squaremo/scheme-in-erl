start <- expr* ws
`hd(Node)`;

ws <- [ \t\n]*
;

expr <- ws quote
      / ws quasiquote / ws unquote / ws unquotesplice
      / ws readermacro
      / ws term
`[_, Expr] = Node, Expr`;

term <- atom / list;

atom <- bool / string / number / symbol;

list <- '(' expr* ws ')'
`[_Open, Es, _, _Close] = Node, Es`;


bool <- true / false;

true  <- '#t' `true`;
false <- '#f' `false`;

string <- '"' chars:(!'"' ("\\\\" / '\\"' / .))* '"'
`iolist_to_binary(proplists:get_value(chars, Node))`;

symbol <- (![0-9-] symbolchar) symbolchar*
`binary_to_atom(iolist_to_binary(Node), utf8)`;

symbolchar <- [a-z] / [A-Z] / [0-9] / [-+_/:=?!<>%&*];

number <- int frac? exp?
`
case Node of
  [Int, [], []] -> list_to_integer(binary_to_list(iolist_to_binary(Int)));
  [Int, Frac, []] -> list_to_float(binary_to_list(iolist_to_binary([Int, Frac])));
  [Int, [], Exp] -> list_to_float(binary_to_list(iolist_to_binary([Int, ".0", Exp])));
  _ -> list_to_float(binary_to_list(iolist_to_binary(Node)))
end
`;

int <- '-'? (non_zero_digit digit+) / digit ~;
frac <- '.' digit+ ~;
exp <- e digit+ ~;
e <- [eE] ('+' / '-')? ~;
non_zero_digit <- [1-9] ~;
digit <- [0-9] ~;

quote <- "'" term
`[_, Term] = Node, [quote, Term]`;

quasiquote <- '`' term
`[_, Term] = Node, [quasiquote, Term]`;

unquote <- ',' term
`[_, Term] = Node, [unquote, Term]`;

unquotesplice <- '@,' term
`[_, Term] = Node, ['unquote-splice', Term]`;

readermacro <- '#' [a-zA-z0-9]+ list
`[_, Sigil, Term] = Node,
 [hashquote, binary_to_atom(iolist_to_binary(Sigil), utf8), Term]`;
